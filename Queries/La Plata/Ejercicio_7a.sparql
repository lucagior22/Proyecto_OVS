PREFIX : <http://www.semanticweb.org/luciana/ontologies/2024/8/inmontology#> 
PREFIX brick: <https://brickschema.org/schema/Brick#> 
PREFIX dc: <http://purl.org/dc/elements/1.1/> 
PREFIX dcterms: <http://purl.org/dc/terms/> 
PREFIX foaf: <http://xmlns.com/foaf/0.1/> 
PREFIX gr: <http://purl.org/goodrelations/v1#> 
PREFIX ns: <http://www.w3.org/2003/06/sw-vocab-status/ns#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX pronto: <https://raw.githubusercontent.com/fdioguardi/pronto/main/ontology/pronto.owl#> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX rec: <https://w3id.org/rec#> 
PREFIX sioc: <http://rdfs.org/sioc/ns#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX time: <http://www.w3.org/2006/time#> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 

# PUNTO 7 - ¿Dentro del partido de La Plata dónde puedo encontrar departamentos a estrenar? -
SELECT ?neighborhood (COUNT(*) as ?count)
WHERE { 
    # Obtenemos los departamentos junto con su ciudad y barrio 
    ?realEstateSite a :Apartment;
        :hasFeature/:hasValue [
            a :PostalAddress;
            :city ?city;
            :neighborhood ?neighborhood
        ].
    
    # Filtramos las ciudades por La Plata
    FILTER(?city = :district_Buenos_Aires_Interior_La_Plata || ?city = :district_Buenos_Aires_La_Plata || ?city = :district_GBA_Sur_La_Plata || ?city = :district_Bs.As._G.B.A._Sur_La_Plata)

    # Obtenemos los listings asociados a los departamentes junto con sus labels
    ?realEstateSite ^sioc:about [
        a pronto:RealEstateListing;
        rdfs:label ?listingLabel;
    ].
    
    # Filtro para departamentos a estrenar con patrones ampliados
    FILTER(REGEX(?listingLabel, "a estrenar|nuevo|sin estrenar|0 km|estrenar|brand new", "i"))
} 
GROUP BY ?neighborhood
